namespace "managarm::posix";

enum Errors {
	SUCCESS = 0,
	DEAD_FORK = 6,
	ILLEGAL_REQUEST = 4,
	ILLEGAL_ARGUMENTS = 9,
	FILE_NOT_FOUND = 1,
	ACCESS_DENIED = 2,
	ALREADY_EXISTS = 3,
	NO_SUCH_FD = 5,
	END_OF_FILE = 7,
	BAD_FD = 8,
	WOULD_BLOCK = 10,
	BROKEN_PIPE = 11,
	NOT_SUPPORTED = 12,
	RESOURCE_IN_USE = 13,
	ILLEGAL_OPERATION_TARGET = 14,
	NOT_A_DIRECTORY = 15,
	NO_BACKING_DEVICE = 16,
	NO_SUCH_RESOURCE = 17,
	INSUFFICIENT_PERMISSION = 18,
	IS_DIRECTORY = 19,
	NOT_A_TTY = 20,
	PROTOCOL_NOT_SUPPORTED = 21,
	ADDRESS_FAMILY_NOT_SUPPORTED = 22,
	NO_MEMORY = 23,
	DIRECTORY_NOT_EMPTY = 24,
	NO_CHILD_PROCESSES = 25,
	SYMBOLIC_LINK_LOOP = 26,
	ALREADY_CONNECTED = 27,
	UNSUPPORTED_SOCKET_TYPE = 28,
	INTERRUPTED = 29,
	NAME_TOO_LONG = 30,
	NO_FILE_DESCRIPTORS_AVAILABLE = 31,
	INTERNAL_ERROR = 99
}

consts CntReqType uint32 {
	INIT = 7,
	FORK = 8,
	EXEC = 1,
	WAIT = 56,
	VM_REMAP = 43,
	VM_PROTECT = 62,
	VM_UNMAP = 27,

	MOUNT = 21,
	CHROOT = 24,
	CHDIR = 58,
	FCHDIR = 61,
	SYMLINKAT = 47,
	READLINK = 31,
	READ = 3,
	WRITE = 4,
	SEEK_ABS = 13,
	SEEK_REL = 14,
	SEEK_EOF = 15,
	DUP = 34,
	DUP2 = 6,
	TTY_NAME = 18,
	GETCWD = 59,
	FD_GET_FLAGS = 44,
	FD_SET_FLAGS = 45,
	GET_RESOURCE_USAGE = 57,

	// Session and process group calls.
	SETSID = 76,

	// Signal-specific calls.
	SIG_ACTION = 55,

	// FIFO- and pipe-specific calls.
	PIPE_CREATE = 52,

	// epoll-specific calls.
	EPOLL_CALL = 54,
	EPOLL_CREATE = 28,
	EPOLL_ADD = 37,
	EPOLL_MODIFY = 38,
	EPOLL_DELETE = 39,
	EPOLL_WAIT = 30,

	// signalfd()-specific calls
	SIGNALFD_CREATE = 32,

	HELFD_ATTACH = 10,
	HELFD_CLONE = 11,

	FD_SERVE = 77
}

@format(bitfield) consts OpenMode uint32 {
	REGULAR = 1,
	HELFD = 2
}

consts FileType uint32 {
	FT_UNKNOWN = 0,
	FT_REGULAR = 1,
	FT_DIRECTORY = 2,
	FT_SYMLINK = 3,
	FT_FIFO = 4,
	FT_SOCKET = 5,
	FT_CHAR_DEVICE = 6,
	FT_BLOCK_DEVICE = 7
}

@format(bitfield) consts OpenFlags uint32 {
	OF_CREATE = 1,
	OF_EXCLUSIVE = 2,
	OF_NONBLOCK = 4,
	OF_CLOEXEC = 256,
	OF_RDONLY = 8,
	OF_WRONLY = 16,
	OF_RDWR = 32,
	OF_TRUNC = 64,
	OF_PATH = 128,
	OF_NOCTTY = 512,
	OF_APPEND = 1024,
	OF_NOFOLLOW = 2048,
	OF_DIRECTORY = 4096
}

@format(bitfield) consts EventFdFlags uint32 {
	CLOEXEC = 1,
	NONBLOCK = 2,
	SEMAPHORE = 4
}

message CntRequest 1 {
head(128):
	CntReqType request_type;

	tags {
		tag(34) int64 pid;

		// used by OPEN
		tag(3) OpenFlags flags;
		tag(10) OpenMode mode;

		// used by READ, WRITE, SEEK_REL, SEEK_ABS, SEEK_END, MMAP, CLOSE, DUP2,
		// HELFD_ATTACH, HELFD_CLONE
		tag(4) int32 fd;

		// used by DUP2
		tag(7) int32 newfd;

		// used by READ
		tag(5) uint32 size;

		// used by SEEK_ABS, SEEK_REL
		tag(11) int64 rel_offset;

		// used by FORK
		tag(8) uint64 child_sp;
		tag(9) uint64 child_ip;

		tag(24) @format(hex) uint64 address;
		tag(23) uint32 new_size;

		// Signal-specific stuff.
		tag(29) uint64 sig_number;
		tag(30) uint64 sig_mask;
		tag(31) @format(hex) uint64 sig_handler;
		tag(32) @format(hex) uint64 sig_restorer;

		tag(22) int32[] fds;
		tag(28) int32[] events;

		// used by EPOLL_CTL
		tag(17) uint64 cookie;

		// used by EPOLL_WAIT
		tag(25) int64 timeout;
		tag(42) int64 sigmask;
		tag(43) int32 sigmask_needed;

		tag(33) uint64 sigset;
		tag(35) char[16] passthrough_credentials;

		tag(36) uint64 cancellation_id;

		tag(37) uint64 signal_seq;
		tag(38) int32 has_signal_seq;
	}
}

message SvrResponse 2 {
head(128):
	tags {
		tag(3) Errors error;

		tag(21) int64 size;

		tag(24) @format(hex) uint64 flags;

		// returned by GET_PID
		tag(18) int64 pid;

		// returned by FSTAT
		tag(5) FileType file_type;

		// returned by FSTAT
		tag(4) uint64 file_size;

		// returned by FSTAT
		tag(13) int32 mode;

		// returned by FSTAT
		tag(22) uint64 fs_devnum;
		tag(14) uint64 fs_inode;

		tag(23) uint64 ref_devnum;

		// returned by FSTAT
		tag(17) uint64 num_links;

		// returned by FSTAT
		tag(15) int64 uid;
		tag(16) int64 gid;

		// returned by FSTAT
		tag(7) int64 atime_secs;
		tag(8) int64 atime_nanos;
		tag(9) int64 mtime_secs;
		tag(10) int64 mtime_nanos;
		tag(11) int64 ctime_secs;
		tag(12) int64 ctime_nanos;

		// return of OPEN
		tag(1) int32 fd;

		// returned by SEEK_ABS, SEEK_REL
		tag(6) @format(hex) uint64 offset;

		// Returned by SETSID.
		tag(31) int64 sid;

		// returned by TTY_NAME
		tag(19) string path;

		tag(20) int32[] fds;
		tag(25) int32[] events;

		// Returned by INOTIFY_ADD.
		tag(30) int32 wd;

		// Signal-specific stuff.
		tag(26) uint64 sig_mask;
		tag(27) uint64 sig_handler;
		tag(28) uint64 sig_restorer;

		// returned by GET_RESOURCE_USAGE
		tag(29) uint64 ru_user_time;

		tag(32) uint32 mount_id;
		tag(33) uint64 stat_dev;
	}
}

message VmMapRequest 3 {
head(128):
	int32 fd;
	uint32 mode;
	uint32 flags;
	@format(hex) uint64 address_hint;
	int64 rel_offset;
	uint64 size;
}

message OpenAtRequest 4 {
head(128):
	int32 fd;
	OpenFlags flags;
	OpenMode mode;
tail:
	string path;
}

message CloseRequest 5 {
head(128):
	int32 fd;
}

message IsTtyRequest 6 {
head(128):
	int32 fd;
}

message RenameAtRequest 7 {
head(128):
	int32 fd;
	int32 newfd;
tail:
	string path;
	string target_path;
}

message GetUidRequest 8 {
head(128):
}

message SetUidRequest 9 {
head(128):
	uint64 uid;
}

message GetEuidRequest 10 {
head(128):
}

message SetEuidRequest 11 {
head(128):
	uint64 uid;
}

message GetGidRequest 12 {
head(128):
}

message GetEgidRequest 13 {
head(128):
}

message SetGidRequest 14 {
head(128):
	uint64 uid;
}

message SetEgidRequest 15 {
head(128):
	uint64 uid;
}

message UnlinkAtRequest 16 {
head(128):
	int32 fd;
	int32 flags;
tail:
	string path;
}

message FstatAtRequest 17 {
head(128):
	int32 fd;
	int32 flags;
tail:
	string path;
}

message MkfifoAtRequest 18 {
head(128):
	int32 fd;
	int32 mode;
tail:
	string path;
}

message LinkAtRequest 19 {
head(128):
	int32 fd;
	int32 newfd;
	int32 flags;
tail:
	string path;
	string target_path;
}

message FchmodAtRequest 20 {
head(128):
	int32 fd;
	int32 flags;
	int32 mode;
tail:
	string path;
}

message UtimensAtRequest 21 {
head(128):
	int32 fd;
	int32 flags;
	int32 mode;
	uint64 atimeSec;
	uint64 atimeNsec;
	uint64 mtimeSec;
	uint64 mtimeNsec;
tail:
	string path;
}

message RmdirRequest 22 {
head(128):
tail:
	string path;
}

message InotifyAddRequest 23 {
head(128):
	int32 fd;
	int32 flags;
tail:
	string path;
}

message InotifyCreateRequest 24 {
head(128):
	int32 flags;
}

message EventfdCreateRequest 25 {
head(128):
	uint32 initval;
	EventFdFlags flags;
}

message SocketRequest 26 {
head(128):
	int32 flags;
	int32 domain;
	int32 socktype;
	int32 protocol;
}

message SockpairRequest 27 {
head(128):
	int32 flags;
	int32 domain;
	int32 socktype;
	int32 protocol;
}

message AcceptRequest 28 {
head(128):
	int32 fd;
}

message MountRequest 29 {
head(128):
tail:
	string path;
	string target_path;
	string fs_type;
}

message SymlinkAtRequest 30 {
head(128):
	int32 fd;
tail:
	string path;
	string target_path;
}

message GetPpidRequest 31 {
head(128):
}

message MknodAtRequest 32 {
head(128):
	int32 dirfd;
	int32 mode;
	int32 device;
tail:
	string path;
}

message GetPgidRequest 33 {
head(128):
	int64 pid;
}

message SetPgidRequest 34 {
head(128):
	int64 pid;
	int64 pgid;
}

message GetSidRequest 35 {
head(128):
	int64 pid;
}

message IoctlFioclexRequest 36 {
head(128):
	int32 fd;
}

message MemFdCreateRequest 37 {
head(128):
	int32 flags;
tail:
	string name;
}

message GetPidRequest 38 {
head(128):
}

message AccessAtRequest 39 {
head(128):
	int32 fd;
	int32 flags;
tail:
	string path;
}

message MkdirAtRequest 40 {
head(128):
	int32 fd;
	int32 mode;
tail:
	string path;
}

message GetAffinityRequest 41 {
head(128):
	int64 pid;
	uint64 size;
}

message SetAffinityRequest 42 {
head(128):
	int64 pid;
tail:
	uint8[] mask;
}

message WaitIdRequest 43 {
head(128):
	uint16 idtype;
	uint64 id;
	int32 flags;
}

message WaitIdResponse 44 {
head(128):
	Errors error;
	int64 pid;
	int64 uid;
	uint64 sig_status;
	uint64 sig_code;
}

message ReadlinkAtRequest 45 {
head(128):
	int32 fd;
tail:
	string path;
}

message NetserverRequest 46 {
head(128):
}

message GetMemoryInformationRequest 91 {
head(128):
}

message GetMemoryInformationResponse 92 {
head(128):
	uint64 total_usable_memory;
	uint64 available_memory;
	uint64 memory_unit;
}

message SysconfRequest 93 {
head(128):
	int32 num;
}

message SysconfResponse 94 {
head(128):
	Errors error;
	int64 value;
}

message RebootRequest 47 {
head(128):
	int64 cmd;
}

message FstatfsRequest 95 {
head(128):
	int64 fd;
tail:
	string path;
}

message FstatfsResponse 96 {
head(128):
	Errors error;
	// Note: In struct statfs, bsize and namelen are 32 bits.
	//       We make them 64 bits for consistency here.
	uint32 fstype;
	uint64 block_size;
	uint64 fragment_size;
	uint64 num_blocks;
	uint64 blocks_free;
	uint64 blocks_free_user;
	uint64 num_inodes;
	uint64 inodes_free;
	uint64 inodes_free_user;
	uint64 max_name_length;
	int32 fsid0;
	int32 fsid1;
	uint64 flags;
}

message ParentDeathSignalRequest 97 {
head(128):
	int32 signal;
}

message ParentDeathSignalResponse 98 {
head(128):
	Errors error;
}

message SetIntervalTimerRequest 99 {
head(128):
	int32 which;
	uint64 value_sec;
	uint64 value_usec;
	uint64 interval_sec;
	uint64 interval_usec;
}

message SetIntervalTimerResponse 100 {
head(128):
	Errors error;
	uint64 value_sec;
	uint64 value_usec;
	uint64 interval_sec;
	uint64 interval_usec;
}

message Dup2Request 101 {
head(128):
	int32 fd;
	int32 newfd;
	int32 flags;
	int32 fcntl_mode;
}

message Dup2Response 102 {
head(128):
	Errors error;
	int32 fd;
}

message InotifyRmRequest 103 {
head(128):
	int32 ifd;
	int32 wd;
}

message InotifyRmReply 104 {
head(128):
	Errors error;
}

message TimerFdCreateRequest 105 {
head(128):
	int32 clock;
	int32 flags;
}

message TimerFdCreateResponse 106 {
head(128):
	Errors error;
	int32 fd;
}

message TimerFdSetRequest 107 {
head(128):
	int32 fd;
	int32 flags;
	uint64 value_sec;
	uint64 value_nsec;
	uint64 interval_sec;
	uint64 interval_nsec;
}

message TimerFdSetResponse 108 {
head(128):
	Errors error;
	uint64 value_sec;
	uint64 value_nsec;
	uint64 interval_sec;
	uint64 interval_nsec;
}

message PidfdOpenRequest 109 {
head(128):
	int64 pid;
	uint32 flags;
}

message PidfdOpenResponse 110 {
head(128):
	Errors error;
	int32 fd;
}

message PidfdSendSignalRequest 111 {
head(128):
	int32 pidfd;
	int32 signal;
	uint32 flags;
}

message PidfdSendSignalResponse 112 {
head(128):
	Errors error;
}

message PidfdGetPidRequest 113 {
head(128):
	int32 pidfd;
}


message PidfdGetPidResponse 114 {
head(128):
	Errors error;
	int64 pid;
}

message TimerFdGetRequest 115 {
head(128):
	int32 fd;
}

message TimerFdGetResponse 116 {
head(128):
	Errors error;
	uint64 value_sec;
	uint64 value_nsec;
	uint64 interval_sec;
	uint64 interval_nsec;
}

message TimerCreateRequest 117 {
head(128):
	int32 clockid;
	int32 sigev_signo;
	int32 sigev_tid;
}

message TimerCreateResponse 118 {
head(128):
	Errors error;
	uint64 timer_id;
}

message TimerSetRequest 119 {
head(128):
	uint64 timer;
	int32 flags;
	uint64 value_sec;
	uint64 value_nsec;
	uint64 interval_sec;
	uint64 interval_nsec;
}

message TimerSetResponse 120 {
head(128):
	Errors error;
	uint64 value_sec;
	uint64 value_nsec;
	uint64 interval_sec;
	uint64 interval_nsec;
}

message TimerGetRequest 121 {
head(128):
	uint64 timer;
}

message TimerGetResponse 122 {
head(128):
	Errors error;
	uint64 value_sec;
	uint64 value_nsec;
	uint64 interval_sec;
	uint64 interval_nsec;
}

message TimerDeleteRequest 123 {
head(128):
	uint64 timer;
}

message TimerDeleteResponse 124 {
head(128):
	Errors error;
}

message UmaskRequest 125 {
head(128):
	int32 newmask;
}

message UmaskResponse 126 {
head(128):
	// umask calls always succeed, no errors needed
	int32 oldmask;
}

message ChdirRequest 127 {
head(128):
tail:
	string path;
}

message ChdirResponse 128 {
head(128):
	Errors error;
}

message ChrootRequest 129 {
head(128):
tail:
	string path;
}

message ChrootResponse 130 {
head(128):
	Errors error;
}

message SetResourceLimitRequest 131 {
head(128):
	int32 resource;
	int64 cur;
	int64 max;
}

message SetResourceLimitResponse 132 {
head(128):
	Errors error;
}

message ProcessDumpableRequest 133 {
head(128):
	byte set;
	int64 new_value;
}

message ProcessDumpableResponse 134 {
head(128):
	Errors error;
	byte value;
}

message GetGroupsRequest 135 {
head(128):
	uint64 size;
}

message GetGroupsResponse 136 {
head(128):
	Errors error;
	uint64 entries;
}

message SetGroupsRequest 137 {
head(128):
	uint64 entries;
}

message SetGroupsResponse 138 {
head(128):
	Errors error;
}

message FchownAtRequest 139 {
head(128):
	int32 fd;
	int32 flags;
	int32 uid;
	int32 gid;
tail:
	string path;
}

message FchownAtResponse 140 {
head(128):
	Errors error;
}
